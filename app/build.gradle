apply plugin: 'com.inspur.comgradle'
apply plugin: 'com.alipay.apollo.baseline.platform'
import com.android.build.OutputFile




/**
 * Set this to true to create two separate APKs instead of one:
 *   - An APK that only works on ARM devices
 *   - An APK that only works on x86 devices
 * The advantage is the size of the APK is reduced by about 4MB.
 * Upload all the APKs to the Play Store and people will download
 * the correct one based on the CPU architecture of their device.
 */
def enableSeparateBuildPerCPUArchitecture = false

/**
 * Run Proguard to shrink the Java bytecode in release builds.
 */
def enableProguardInReleaseBuilds = false
Properties properties = new Properties()
properties.load(new FileInputStream(rootProject.projectDir.parent+SIGN_PROPERTIES))
android {
    signingConfigs {
        config {
            keyAlias properties.getProperty("KEY_ALIAS")
            keyPassword properties.getProperty("KEY_PASSWORD")
            storeFile file(properties.getProperty("KEY_STORE_FILE_PATH"))
            storePassword properties.getProperty("KEY_PASSWORD")
        }
    }
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.androidTools
    aaptOptions.cruncherEnabled = false
    aaptOptions.useNewCruncher = false
    def isDebug = false
    for(String s : gradle.startParameter.taskNames) {
        if (s.contains("Debug") | s.contains("debug")) {
            isDebug = true
            break
        }
    }

    if (isDebug) {
        splits.abi.enable = false
        splits.density.enable = false
    }else {
        apply from: "../node_modules/react-native/react.gradle"
        apply from: "../node_modules/react-native-vector-icons/fonts.gradle"
        apply from: 'tinkerpatch.gradle'
    }
    defaultConfig {
        applicationId "com.inspur.emmcloud"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
//        versionCode 128 升级更新以version为判断依据
        versionCode 178
        //版本号范例：1.0.0-beta.1, 1.0.0-rc.1, 1.0.0
//        versionName "4.8.19"
        versionName "5.1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters "armeabi-v7a", "x86", 'arm64-v8a'
        }
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
//        manifestPlaceholders = [
//                PKG_NAME:applicationId
//        ]
    }
    sourceSets.main {
        jniLibs.srcDirs = ['libs']
    }
    buildTypes {
        release {
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.config
        }
    }
    lintOptions {
        lintConfig file("lint.xml")
        abortOnError false
        checkReleaseBuilds false
        disable 'MissingTranslation', 'ExtraTranslation','TypographyFractions','TypographyQuotes'
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name == "release") {
            variant.outputs.all { output ->
                def outputFile = output.outputFile
                def fileName
                if (variant.buildType.name.equals('release')) {
                    fileName = "inspur_cloud_mobileclient_${defaultConfig.versionName}.apk"
                } else if (variant.buildType.name.equals('debug')) {
                    fileName = "inspur_cloud_mobileclient_debug_${defaultConfig.versionName}.apk"
                }
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    outputFileName = fileName
                }
            }
        }
    }

    splits {
        abi {
            reset()
            enable true
            universalApk true  // If true, also generate a universal APK
            include "armeabi-v7a", "x86", "arm64-v8a"
        }
    }
    // applicationVariants are e.g. debug, release
    //android studio对于jni调用及运行闪退无法加载库的问题解决方案
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            // For each separate APK per architecture, set a unique version code as described here:
            // http://tools.android.com/tech-docs/new-build-system/user-guide/apk-splits
            def versionCodes = ["armeabi-v7a": 1, "x86": 2, "arm64-v8a": 3]
            def abi = output.getFilter(OutputFile.ABI)
            if (abi != null) {  // null for the universal-debug, universal-release variants
                output.versionCodeOverride =
                        versionCodes.get(abi) * 1048576 + defaultConfig.versionCode
            }
        }
    }
    if (isDebug){
        tasks.whenTaskAdded { task ->
            if (task.name.contains("lint")
                    ||task.name.contains("Aidl")//用不到测试的时候就可以先关闭
                    ||task.name.contains("mockableAndroidJar")
                    ||task.name.contains("UnitTest")
                    ||task.name.contains("AndroidTest")//用不到NDK和JNI的也关闭掉
            ) {
                task.enabled = false
            }
            if (task.name.startsWith("merge") && task.name.endsWith("NativeLibs")) {
                task.doFirst {
                    def startTime = System.currentTimeMillis()
                    println "Support 64-bit => abi start."
                    List<String> armeabi_v7a = []
                    List<String> arm64_v8a = []
                    it.inputs.files.each { file ->
                        findSoFile(file, armeabi_v7a, arm64_v8a)
                    }
                    println("v7a size: ${armeabi_v7a.size()}")
                    println("arm64 size: ${arm64_v8a.size()}")
                    println("so in v7a, but not in arm64:")
                    armeabi_v7a.each {
                        if (!arm64_v8a.contains(it)) {
                            println("$it")
                        }
                    }
                    def endTime = System.currentTimeMillis()
                    println("${task.name} cost time ${endTime - startTime}ms")
                }
            }
        }
    }
}

def findSoFile(File file, List<String> armeabi_v7a, List<String> arm64_v8a) {
    if (null == file) {
        return
    }
    if (file.isDirectory()) {
        file.listFiles().each {
            findSoFile(it, armeabi_v7a, arm64_v8a)
        }
    } else if (file.absolutePath.endsWith(".so")) {
        def soPath = file.absolutePath
        def soName = file.name
        println "Support 64-bit => so: ${file.absolutePath}   ${file.name}"
        // 如果你的so库存放文件夹名字不是这个,修改成你的文件夹名字
        if (soPath.contains("armeabi-v7a")) {
            armeabi_v7a.add(soName)
        } else if (soPath.contains("arm64-v8a")) {
            arm64_v8a.add(soName)
        }
    }
}

combuild {
    applicationName = 'com.inspur.emmcloud.MyApplication'
    isRegisterCompoAuto = false
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':basemodule')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    annotationProcessor deps.ARouter.router_anno
    implementation deps.butterknife.butterknife
    annotationProcessor deps.butterknife.butterknife_compiler

    implementation deps.react
    // React Naive Components
    implementation project(':react-native-smart-swipe-refresh-layout')
    implementation project(':react-native-picker')
    implementation project(':react-native-svg')
    implementation project(':react-native-vector-icons')
    implementation project(':react-native-navigation')
    //无需引入tinker的任何库,使用tinkerpatch sdk即可
    implementation("com.tinkerpatch.sdk:tinkerpatch-android-sdk:${TINKERPATCH_VERSION}") {
        changing = true
    }
}

//class BuildTimeListener implements TaskExecutionListener,BuildListener{
//    private Clock clock;
//    private times = []
//
//    @Override
//    void buildStarted(Gradle gradle) {
//    }
//
//    @Override
//    void beforeExecute(Task task) {
//        clock = new org.gradle.util.Clock()
//    }
//
//    @Override
//    void settingsEvaluated(Settings settings) {
//    }
//
//    @Override
//    void afterExecute(Task task, TaskState state) {
//        def ms = clock.getTimeInMs()
//        times.add([ms, task.path])
//    }
//
//    @Override
//    void projectsLoaded(Gradle gradle) {
//    }
//
//    @Override
//    void projectsEvaluated(Gradle gradle) {
//    }
//
//    @Override
//    void buildFinished(BuildResult result) {
//        println "Task spend time:====================================="
//        for (time in times) {
//            if (time[0] >= 50) {
//                printf "%7sms  %s\n", time
//            }
//        }
//    }
//}
//
////将监听器添加到 gradle任务中
//project.gradle.addListener(new BuildTimeListener());